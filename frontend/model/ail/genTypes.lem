open import Pervasives AilSyntax Ctype

type genIntegerType [name="git*"] =
 | Concrete of integerType
 | SizeT
 | PtrdiffT
 | PtraddrT
 | Unknown of integerConstant
 | Promote of genIntegerType
 | Usual of genIntegerType * genIntegerType


type genBasicType [name="gbt"] =
 | GenInteger of genIntegerType
 | GenFloating of floatingType

type genType =
 | GenVoid
 | GenBasic of genBasicType
 | GenArray of ctype * (maybe integer)
 | GenFunction of (qualifiers * ctype)
              * list (qualifiers * ctype * (* is_register *)bool)
              * (* is_variadic *)bool
 | GenFunctionNoParams of (qualifiers * ctype) (* function type with no `prototype' *)
 | GenPointer of qualifiers * ctype
 | GenStruct of ail_identifier
 | GenUnion of ail_identifier
 | GenAtomic of genType

val signedInt_gty: genType
let signedInt_gty =
  GenBasic (GenInteger (Concrete (Signed Int_)))

val genChar16_t: genType
let genChar16_t =
  (* STD ยง7.28#2 *)
  GenBasic (GenInteger (Concrete (Unsigned (Int_leastN_t 16))))

val genChar32_t: genType
let genChar32_t =
  (* STD ยง7.28#2 *)
  GenBasic (GenInteger (Concrete (Unsigned (Int_leastN_t 32))))


(* TODO: is this ever used? *)
type typeCategory [name = "tc*"] = 
 | LValueType of qualifiers * ctype * (* isRegister *) bool
 | RValueType of ctype


type genTypeCategory =
 | GenLValueType of qualifiers * ctype * (* isRegister *) bool
 | GenRValueType of genType


(* Equality functions *)
let rec eq_genIntegerType gity1 gity2 =
  let ord = function
    | Concrete _ ->
        (0 : nat)
    | SizeT ->
        1
    | PtrdiffT ->
        2
    | PtraddrT ->
        3
    | Unknown _ ->
        4
    | Promote _ ->
        5
    | Usual _ _ ->
        6
  end in
  match (gity1, gity2) with
    | (Concrete ity1, Concrete ity2) ->
        ity1 = ity2
    | (Unknown iCst1, Unknown iCst2) ->
        iCst1 = iCst2
    | (Promote gity1, Promote gity2) ->
        eq_genIntegerType gity1 gity2
    | (Usual gity11 gity21, Usual gity12 gity22) ->
        eq_genIntegerType gity11 gity12 && eq_genIntegerType gity21 gity22
    | _ ->
        ord gity1 = ord gity2
  end

instance (Eq genIntegerType)
  let (=)  = eq_genIntegerType
  let (<>) = fun git1 git2 -> not (eq_genIntegerType git1 git2)
end


let eq_genBasicType gbty1 gbty2 =
  match (gbty1, gbty2) with
    | (GenInteger gity1, GenInteger gity2) ->
        eq_genIntegerType gity1 gity2
    | (GenFloating fty1, GenFloating fty2) ->
        fty1 = fty2
    | _ ->
        false
  end

instance (Eq genBasicType)
  let (=)  = eq_genBasicType
  let (<>) = fun gbty1 gbty2 -> not (eq_genBasicType gbty1 gbty2)
end


let rec eq_genType gty1 gty2 =
  let ord = function
    | GenVoid ->
        (0 : nat)
    | GenBasic _ ->
        1
    | GenArray _ _ ->
        2
    | GenFunction _ _ _ ->
        3
    | GenFunctionNoParams _ ->
        4
    | GenPointer _ _ ->
        5
    | GenStruct _ ->
        6
    | GenUnion _ ->
        7
    | GenAtomic _ ->
        8
  end in
  match (gty1, gty2) with
    | (GenBasic gbty1, GenBasic gbty2 ) ->
        gbty1 = gbty2
    | (GenArray ty1 n1_opt, GenArray ty2 n2_opt) ->
        ty1 = ty2 && n1_opt = n2_opt
    | (GenFunction (qs1, ty1) params1 b1,
       GenFunction (qs2, ty2) params2 b2 ) ->
        qs1 = qs2 && ty1 = ty2 && params1 = params2 && b1 = b2
    |  (GenFunctionNoParams (qs1, ty1), GenFunctionNoParams (qs2, ty2)) ->
        qs1 = qs2 && ty1 = ty2
    | (GenPointer qs1 ty1, GenPointer qs2 ty2) ->
        qs1 = qs2 && ty1 = ty2
    | (GenStruct tag1, GenStruct tag2) ->
        tag1 = tag2
    | (GenUnion tag1, GenUnion tag2) ->
        tag1 = tag2
    | (GenAtomic gty1, GenAtomic gty2) ->
        eq_genType gty1 gty2
    | _ ->
        ord gty1 = ord gty2
  end

instance (Eq genType)
  let (=)  = eq_genType
  let (<>) = fun gty1 gty2 -> not (eq_genType gty1 gty2)
end

let eq_genTypeCategory gentyCat1 gentyCat2 =
  let ord = function
    | GenLValueType _  _ _ ->
        (0 : nat)
    | GenRValueType _ ->
        1
  end in
  match (gentyCat1, gentyCat2) with
    | (GenLValueType qs1 ty1 isRegister1, GenLValueType qs2 ty2 isRegister2) ->
        qs1 = qs2 && ty1 = ty2 && isRegister1 = isRegister2
    | (GenRValueType gty1, GenRValueType gty2) ->
        gty1 = gty2
    | _ ->
        ord gentyCat1 = ord gentyCat2
  end

instance (Eq genTypeCategory)
  let (=)  = eq_genTypeCategory
  let (<>) = fun gty1 gty2 -> not (eq_genTypeCategory gty1 gty2)
end


(* TODO: documentation *)
val inject_integerType: integerType -> genIntegerType
let inject_integerType ity =
  Concrete ity

val inject_basicType: basicType -> genBasicType
let inject_basicType = function
  | Integer ity ->
      GenInteger (inject_integerType ity)
  | Floating fty ->
      GenFloating fty
end

val     inject_type: ctype -> genType
let rec inject_type (Ctype _ ty) =
match ty with
  | Void -> GenVoid
  | Basic bty ->
      GenBasic (inject_basicType bty)
  | Pointer qs ty ->
      GenPointer qs ty
  | Array ty n_opt ->
      GenArray ty n_opt
  | Function qs_ty params b ->
      GenFunction qs_ty params b
  | FunctionNoParams qs_ty ->
      GenFunctionNoParams qs_ty
  | Atomic ty ->
      GenAtomic (inject_type ty)
  | Struct tag ->
      GenStruct tag
  | Union  tag ->
      GenUnion tag
end

val inject_typeCategory: typeCategory -> genTypeCategory
let inject_typeCategory = function
  | LValueType qs ty isRegister ->
      GenLValueType qs ty isRegister
  | RValueType ty ->
      GenRValueType (inject_type ty)
end

val annotations: forall 'a. expression 'a -> (list Annot.annot * 'a)
let annotations (AnnotatedExpression gtc annots _ _) = (annots, gtc)

val genTypeCategoryOf: expression genTypeCategory -> genTypeCategory
let genTypeCategoryOf (AnnotatedExpression gtc _ _ _) =
  gtc

