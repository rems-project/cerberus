open import Maybe
open import Pervasives
import Loc
import Symbol
import IntegerType

type bmc_annot =
  | Abmc_id of nat (* NOTE: basically same as uid *)

type attribute = <|
  attr_ns: maybe Symbol.identifier;
  attr_id: Symbol.identifier;
  attr_args: list (Loc.t * string * list (Loc.t * string));
|>

type attributes =
  | Attrs of list attribute

instance (Show attributes)
  let show attrs = "ATTRS"
end

val no_attributes: attributes
let no_attributes =
  Attrs []

val combine_attributes: attributes -> attributes -> attributes
let combine_attributes (Attrs xs1) (Attrs xs2) =
  Attrs (xs1 ++ xs2)

type loop_id = nat

(* records where a label comes from *)
type label_annot = 
  | LAloop of loop_id
  | LAloop_continue of loop_id
  | LAloop_break of loop_id
  | LAreturn (* when an Esave is annotated with this it indicates it is the
                return label *)
  | LAswitch
  | LAcase
  | LAdefault

  | LAactual_label

instance (Show label_annot)
  let show = function
    | LAloop id -> "loop " ^ show id
    | LAloop_continue id -> "loop continue " ^ show id
    | LAloop_break id -> "loop break " ^ show id
    | LAreturn -> "return"
    | LAswitch -> "switch"
    | LAcase -> "case"
    | LAdefault -> "default"
    | LAactual_label -> "label"
  end
end

type cerb_attribute =
  | ACerb_with_address of integer
  | ACerb_hidden

type value_annot =
  | Ainteger of IntegerType.integerType

type annot =
  | Astd of string (* ISO C11 Standard Annotation *)
  | Aloc of Loc.t (* C source location *)
  | Auid of string (* Unique ID *)
  | Amarker of nat
  | Amarker_object_types of nat
  | Abmc of bmc_annot
  | Aattrs of attributes (* C2X attributes *)
  | Atypedef of Symbol.sym (* (TODO: I don't like but hey)
                              must only be used on a ctype to indicate it is a unfolding of a typedef *)
  | Alabel of label_annot
  | Acerb of cerb_attribute
  | Avalue of value_annot
  | Ainlined_label of (Loc.t * Symbol.sym * label_annot)
  | Astmt (* Added for CN, to mark an Ail statement boundary *)
  | Aexpr (* Added for CN, to mark an Ail expression boundary *)


type identifier_item_kind =
  | Marker_Local
  | Marker_Global

type identifier_env =
  map Symbol.identifier (maybe (identifier_item_kind * Symbol.sym))

type loop_attribute =
  <| marker_id : nat;
     attributes : attributes;
     loc_condition : Loc.t;
     loc_loop : Loc.t; |>

type loop_attributes = map loop_id loop_attribute
(*type loop_attributes = map loop_id (nat * attributes * Loc.t * Loc.t)*) (* nat is marker id, first loc is location of loop condition, second loc is location of whole loop *)

val     get_loc: list annot -> maybe Loc.t
let rec get_loc annots =
  match annots with
    | [] ->
        Nothing
    | (Aloc loc :: _) ->
        Just loc
    | (Astd _ :: annots') ->
        get_loc annots'
    | (Auid _ :: annots') ->
        get_loc annots'
    | (Amarker _ :: annots') ->
        get_loc annots'
    | (Amarker_object_types _ :: annots') ->
        get_loc annots'
    | (Abmc _ :: annots') ->
        get_loc annots'
    | (Aattrs _ :: annots') ->
        get_loc annots'
    | (Atypedef _ :: annots') ->
        get_loc annots'
    | (Alabel _ :: annots') ->
        get_loc annots'
    | (Acerb _ :: annots') ->
        get_loc annots'
    | (Avalue _ :: annots') ->
        get_loc annots'
    | (Ainlined_label _ :: annots') ->
        get_loc annots'
    | (Astmt :: annots') ->
        get_loc annots'
    | (Aexpr :: annots') ->
        get_loc annots'
  end

(* This is assuming there is only one Atypedef annot *)
val     get_typedef: list annot -> maybe Symbol.sym
let rec get_typedef annots =
  match annots with
    | [] ->
        Nothing
    | (Atypedef sym :: _) ->
        Just sym
    | _ :: annots' ->
        get_typedef annots'
  end

val get_loc_: list annot -> Loc.t
let get_loc_ annots =
  match get_loc annots with
    | Just loc -> loc
    | Nothing -> Loc.unknown
  end

val only_loc: list annot -> list annot
let only_loc annots = 
  List.filter (function Aloc _ -> true | _ -> false end) annots



val     get_attrs: list annot -> maybe attributes
let rec get_attrs annots = 
  match annots with
    | [] ->
        Nothing
    | (Aloc loc :: annots') ->
        get_attrs annots'
    | (Astd _ :: annots') ->
        get_attrs annots'
    | (Auid _ :: annots') ->
        get_attrs annots'
    | (Amarker _ :: annots') ->
        get_attrs annots'
    | (Amarker_object_types _ :: annots') ->
        get_attrs annots'
    | (Abmc _ :: annots') ->
        get_attrs annots'
    | (Atypedef _ :: annots') ->
        get_attrs annots'
    | (Aattrs (Attrs attributes) :: annots') ->
       match get_attrs annots' with
       | Just (Attrs attributes') -> Just (Attrs (attributes ++ attributes'))
       | Nothing -> Just (Attrs attributes)       
       end
    | (Alabel _ :: annots') ->
        get_attrs annots'
    | (Acerb _ :: annots') ->
        get_attrs annots'
    | (Avalue _ :: annots') ->
        get_attrs annots'
    | (Ainlined_label _ :: annots') ->
        get_attrs annots'
    | (Astmt :: annots') ->
        get_attrs annots'
    | (Aexpr :: annots') ->
        get_attrs annots'
  end


let get_cerb_magic_attr annots =
  match get_attrs annots with
  | Nothing -> []
  | Just (Attrs attributes) ->
     List.concatMap (fun attr ->
         match (attr.attr_ns, attr.attr_id) with
         | (Just (Symbol.Identifier _loc1 "cerb"), Symbol.Identifier _loc2 "magic") ->
            List.map (fun (loc, arg, _) -> (loc, arg)) attr.attr_args
         | _ ->
            []
         end
       ) attributes
  end

       





val     get_marker: list annot -> maybe nat
let rec get_marker annots = 
  match annots with
    | [] ->
        Nothing
    | (Amarker id :: annots') ->
       match get_marker annots' with
       | Just _ -> Assert_extra.failwith "found more than one marker"
       | Nothing -> Just id
       end
    | (_ :: annots') ->
       get_marker annots'
  end

val     get_marker_object_types: list annot -> maybe nat
let rec get_marker_object_types annots = 
  match annots with
    | [] ->
        Nothing
    | (Amarker_object_types id :: annots') ->
       match get_marker_object_types annots' with
       | Just _ -> Assert_extra.failwith "found more than one marker (object type marker)"
       | Nothing -> Just id
       end
    | (_ :: annots') ->
       get_marker_object_types annots'
  end


val get_label_annot : list annot -> maybe label_annot
let rec get_label_annot annots = 
  match annots with
  | [] -> Nothing
  | Alabel la :: _ -> Just la
  | _ :: annots -> get_label_annot annots
  end


val     get_uid: list annot -> maybe string
let rec get_uid annots =
  match annots with
    | [] ->
        Nothing
    | (Aloc _ :: annots') ->
        get_uid annots'
    | (Astd _ :: annots') ->
        get_uid annots'
    | (Auid uid :: _) ->
        Just uid
    | (Amarker _ :: annots') ->
        get_uid annots'
    | (Amarker_object_types _ :: annots') ->
        get_uid annots'
    | (Abmc _ :: annots') ->
        get_uid annots'
    | (Atypedef _ :: annots') ->
        get_uid annots'
    | (Aattrs _ :: annots') ->
        get_uid annots'
    | (Alabel _ :: annots') ->
        get_uid annots'
    | (Acerb _ :: annots') ->
        get_uid annots'
    | (Avalue _ :: annots') ->
        get_uid annots'
    | (Ainlined_label _ :: annots') ->
        get_uid annots'
    | (Astmt :: annots') ->
        get_uid annots'
    | (Aexpr :: annots') ->
        get_uid annots'
  end


val is_return: list annot -> bool
let is_return annots =
  get_label_annot annots = Just LAreturn

val is_loop_break: list annot -> bool
let is_loop_break annots =
  match get_label_annot annots with
  | Just (LAloop_break _) -> true
  | _ -> false
  end




val set_loc : Loc.t -> list annot -> list annot
let set_loc loc annots = 
  let annots' = 
    List.mapMaybe (function
        | Aloc l -> Nothing
        | a -> Just a
    end) annots
  in
  Aloc loc :: annots'
