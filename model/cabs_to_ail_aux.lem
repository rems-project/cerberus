open import Pervasives
open import Utils Cabs

import AilSyntax AilTypes

module A = AilSyntax
module ATypes = AilTypes


type tag_definition =
  | Struct_definition of bool(* isAnonymous*) * list (Cabs.cabs_identifier * (ATypes.qualifiers * ATypes.ctype))
  | Union_definition of bool(* isAnonymous*) * list (Cabs.cabs_identifier * (ATypes.qualifiers * ATypes.ctype))
  | Enum_definition of list integer


(* STD ยง6.2.2#1 *)
type linkage =
  | Linkage_external
  | Linkage_internal
  | Linkage_none

instance (Eq linkage)
  let (=)  = unsafe_structural_equality
  let (<>) = unsafe_structural_inequality
end

instance (Show linkage)
  let show = function
    | Linkage_external ->
        "external"
    | Linkage_internal ->
        "internal"
    | Linkage_none ->
        "none"
  end
end

instance (SetType linkage)
  let setElemCompare = defaultCompare
end



val specifiers_of_external_declaration: external_declaration -> maybe specifiers
let specifiers_of_external_declaration = function
  | EDecl_func (FunDef _ specifs _ _) ->
      Just specifs
  | EDecl_decl (Declaration_base specifs _) ->
      Just specifs
  | EDecl_decl (Declaration_static_assert _) ->
      Nothing
end


val determinate_storage_duration: linkage -> list storage_class_specifier -> ATypes.storageDuration
let determinate_storage_duration link scs =
  if not (List.elem SC_Thread_local scs) &&
     (link = Linkage_external || link = Linkage_internal ||
      List.elem SC_static scs) then
(* STD ยง6.2.4#3, sentence 1 *)
    ATypes.Static
  
  else if List.elem SC_Thread_local scs then
(* STD ยง6.2.4#4, sentence 1 *)
    ATypes.Thread
  
  else if link = Linkage_none && not (List.elem SC_static scs) then
(* STD ยง6.2.4#5, sentence 1 *)
    ATypes.Automatic
  
  else
    error "determinate_storage_duration"


let string_of_storageDuration = function
  | ATypes.Static ->
      "static"
  | ATypes.Thread ->
      "thread"
  | ATypes.Automatic ->
      "automatic"
  | ATypes.Allocated ->
      "allocated"
end


val mk_zeroInit: map A.identifier tag_definition -> ATypes.ctype -> A.expression unit
let rec mk_zeroInit_aux tagDefs (ATypes.Ctype _ ty) =
  match ty with
    | ATypes.Void ->
        error "[Cabs_to_ail.mk_zeroInit_aux] - internal ERROR: Void"
    | ATypes.Basic (ATypes.Integer _) ->
        A.ConstantInteger (A.IConstant 0 A.Octal Nothing)
    | ATypes.Basic (ATypes.Floating _) ->
        A.ConstantFloating ("0.0", Nothing)
    | ATypes.Array _ Nothing ->
        error "[Cabs_to_ail.mk_zeroInit_aux] - internal ERROR: Array Nothing"
    | ATypes.Array elem_ty (Just n) ->
        A.ConstantArray elem_ty begin
          List.replicate (natFromInteger n) (mk_zeroInit_aux tagDefs elem_ty)
        end
    | ATypes.Function _ _ _ _ ->
        error "[Cabs_to_ail.mk_zeroInit_aux] - internal ERROR: Function"
    | ATypes.Pointer _ _ ->
        A.ConstantNull
    | ATypes.Atomic atom_ty ->
        (* TODO: check *)
        mk_zeroInit_aux tagDefs atom_ty
    | ATypes.Struct tag_sym ->
        A.ConstantStruct tag_sym
          match Map.lookup tag_sym tagDefs with
            | Just (Struct_definition _ xs) ->
                List.map (fun (memb_ident, (_, memb_ty)) ->
                  (memb_ident, mk_zeroInit_aux tagDefs memb_ty)
                ) xs
            | _ ->
                error "[Cabs_to_ail.mk_zeroInit_aux] - internal ERROR: Struct"
          end
    | ATypes.Union tag_sym ->
        match Map.lookup tag_sym tagDefs with
          | Just (Union_definition _ ((memb_ident, (_, memb_ty)) :: _)) ->
              A.ConstantUnion tag_sym memb_ident (mk_zeroInit_aux tagDefs memb_ty)
          | _ ->
              error "[Cabs_to_ail.mk_zeroInit_aux] - internal ERROR: Union"
        end
    | ATypes.Builtin str ->
        error ("WIP[Cabs_to_ail.mk_zeroInit_aux], Builtin: " ^ str)
  end
let mk_zeroInit tagDefs ty =
  A.AnnotatedExpression () [] Loc.unknown
    (A.AilEconst (mk_zeroInit_aux tagDefs ty))

open AilSyntax
val zeroAil: expression unit
let zeroAil =
  AnnotatedExpression () [] Loc.unknown
    (AilEconst (ConstantInteger (IConstant 0 Octal Nothing)))

val oneAil: expression unit
let oneAil =
  AnnotatedExpression () [] Loc.unknown
    (AilEconst (ConstantInteger (IConstant 1 Decimal Nothing)))
